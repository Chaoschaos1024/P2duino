_clkfreq = 320_000_000

VAR buff[256]

PUB go() | Phase, Head, Tail, Diff, HeadPtr, HeadData, TailPtr, TailPtrOpp, TailData, TailDataOpp, Fade, FadeOpp, Modulus, ModulusOpp, FinalData

  'Configure PLOT
  debug("`plot p pos 330 1600 size 350 350 update")
  debug("`p offset 175 175 polar -256 textsize 14 textstyle 1")

  'Configure SCOPE
  debug("`scope s pos 700 1600 size 700 810 samples 701 rate 1 linesize 3 textsize 12")
  debug("`s 'HeadData' -10000 10000 170 620 15 $FF0000")
  debug("`s 'TailData' -10000 10000 170 420 15 $00FFFF")
  debug("`s 'Weight' 0 128 170 420 0 $007F7F")
  debug("`s 'TailDataOpp' -10000 10000 170 220 15 $FFFF00")
  debug("`s 'WeightOpp' 0 128 170 220 0 $7F7F00")
  debug("`s 'Final' -10000 10000 170 20 15 $00FF00")
  debug("`s 'Mod' -10000 10000 170 20 15 $003F3F")
  debug("`s 'ModOpp' -10000 10000 170 20 15 $3F3F00")

  'Configure FFT
  debug("`fft f pos 40 1600 size 256 200 samples 256 rate 1 textsize 12 logscale")
  debug("`f 'Final' 0 10000 170 15 15")

  'Configure SPECTRO
  debug("`spectro t pos 380 1983 depth 300 samples 256 rate 10 luma8x %100 mag 0 range 1000 logscale", dly(200))

  'Demonstrate concept
  repeat

    'Write head data into buffer
    HeadPtr := Head & $FF
    HeadData,_ := polxy(10000, Phase += $01B9_0000)
    buff[HeadPtr] := HeadData

    'Crossfade tail data and tail-opposite data to make final data
    TailPtr := Tail >> 8 & $FF
    TailPtrOpp := (TailPtr + $80) & $FF
    TailData := buff[TailPtr]
    TailDataOpp := buff[TailPtrOpp]
    Diff := (HeadPtr - TailPtr) & $FF
    Fade := lookup(Diff: 0..127, 128..1)
    FadeOpp := 128 - Fade
    Modulus := (TailData * Fade) / 128
    ModulusOpp := (TailDataOpp * FadeOpp) / 128
    FinalData := Modulus + ModulusOpp

    'Update PLOT
    debug("`p clear")
    debug("`p text 150 -64 20 3 $FFFF00 'Crossfade Pitchbend'")
    debug("`p text 150 64 14 1 $404040 'Circular Buffer'")
    debug("`p linesize 3 linecolor $404040 oval 0 0 256 256")
    debug("`p linesize 6 linecolor $FF0000 0 0 to 128 ", sdec_(head & $FF), " text 'Head'")
    debug("`p linesize 3 linecolor $404040 -128 ", sdec_(TailPtr), " to 128 ", sdec_(TailPtr))
    debug("`p linesize 6 linecolor $00FF00 ", sdec_(Fade, TailPtr), " to ", sdec_(Fade - 128, TailPtr))
    debug("`p text 64 ", sdec_(TailPtr), " 11 0 '", sdec_(Fade * 100 / 128), "%'")
    debug("`p text 64 ", sdec_(TailPtrOpp), " 11 0 '", sdec_(FadeOpp * 100 / 128), "%'")
    debug("`p text 128 ", sdec_(TailPtr), " 'Tail'")
    debug("`p text 128 ", sdec_(TailPtrOpp), " 9 0 'Opp'")
    debug("`p update")

    'Update SCOPE
    debug("`s ", sdec_(HeadData, TailData, Fade, TailDataOpp, FadeOpp, FinalData, Modulus, ModulusOpp))

    'Update FFT and SPECTRO
    debug("`f t ", sdec_(FinalData))

    'slow down
    waitms(2)

    'update head and tail
    Head++
    Tail += 407-300
